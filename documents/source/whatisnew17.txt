.. index:: 
	single: What is new in Ring 1.17; Introduction

========================
What is new in Ring 1.17
========================

In this chapter we will learn about the changes and new features in Ring 1.17 release.

.. index:: 
	pair: What is new in Ring 1.17?; List of changes and new features

List of changes and new features
================================

Ring 1.17 comes with the next features!

* CSVLib Library
* JSONLib Library
* TokensLib Library
* More Improvements

.. index:: 
	pair: What is new in Ring  1.17; CSVLib Library

CSVLib Library
==============

Using this library we can generate and read CSV files 

Functions:

.. code-block:: none

	List2CSV(aList) --> cCSVString
	CSV2List(cCSVString) --> aList


Example:

.. code-block:: ring

	load "csvlib.ring"

	aList = [ ["number", "square" ] ]

	for t=1 to 10
		aList + [ t, t*t ]
	next
	
	write( "squares.csv", list2CSV(aList) )

Output:

.. image:: squarestable.png
	:alt: squarestable


Example (2)

.. code-block:: ring

	load "csvlib.ring"

	if ! fexists("squares.csv")
		? "The file squares.csv doesn't exist! - Run writeSquaresTable.ring to create it"
		return
	ok
	
	aList = CSV2List( read("squares.csv") )
	
	for subList in aList 
		? "" + subList[1] + " - " + subList[2]
	next

Output:

.. code-block:: none

	number - square
	1 - 1
	2 - 4
	3 - 9
	4 - 16
	5 - 25
	6 - 36
	7 - 49
	8 - 64
	9 - 81
	10 - 100

.. index:: 
	pair: What is new in Ring  1.17; JSONLib Library

JSONLib Library
===============

Using this library we can generate and read JSON files 

Functions:

.. code-block:: none

	List2JSON(aList) --> cJSONString
	JSON2List(cJSONString) --> aList


Example (1):

File: sample.json

.. code-block:: none

	{
	  "firstName": "John",
	  "lastName": "Smith", 
	  "age": 20,
	  "address": {
		"streetAddress": "21 2nd Street",
		"city": "New York",
		"state": "NY",
		"postalCode": "10021"
	  },
	  "phoneNumbers": [
		{ "type": "home", "number": "212 555-1234" },
		{ "type": "fax", "number": "646 555-4567" }
	  ]
	}

Ring Code:

.. code-block:: none

	load "jsonlib.ring"
	
	func main
	
		aList = JSON2List( read("sample.json") )
	
		? aList[:FirstName]
		? aList[:LastName]
		? aList[:Age]
		? aList[:Address][:city]
		? aList[:phoneNumbers][1][:Type]
		? aList[:phoneNumbers][1][:Number]
		? aList[:phoneNumbers][2][:Type]
		? aList[:phoneNumbers][2][:Number]

Output:

.. code-block:: none

	John
	Smith
	20
	New York
	home
	212 555-1234
	fax
	646 555-4567

Example (2):

.. code-block:: ring

	load "jsonlib.ring"
	
	func main
	
		aList = [
			:name = "Ring",
			:year = 2016
		]
	
		? List2JSON(aList)

Output:

.. code-block:: none

	{
	        "name": "Ring",
	        "year": 2016
	}

.. index:: 
	pair: What is new in Ring  1.17; TokensLib Library

TokensLib Library
=================

Using this library we can get Ring tokens from source code files or strings.

Example:

.. code-block:: ring

	load "tokenslib.ring"

	func main
		oTokens = new RingTokens {
			fromFile("hello.ring")
			PrintTokens()
			? Copy("=",50)
			fromString("? 1+1")
			PrintTokens()
		}

Output:

.. code-block:: none

	Keyword     : SEE
	Literal     : Hello, World!
	EndLine
	==================================================
	Operator    : ? (23)
	Number      : 1
	Operator    : + (1)
	Number      : 1
	EndLine

.. index:: 
	pair: What is new in Ring  1.17; More Improvements

More Improvements
=================

* Many documents and samples are revised (Better English)
* RingLibSDL - LibSDL version is updated from 2.0.14 to 2.0.18
* RingAllegro - Allegro version is updated from 5.2.4 to 5.2.7.1
* Ring Object File - ring_objfile_writeCfile() - Better Code